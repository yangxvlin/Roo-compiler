procedure main ()
    integer m_hi, n_hi, m, n, v;
    boolean grid;
{
    writeln "Ackermann function calculator";
    writeln "Enter each value and press <ENTER>";
    write "Compute grid? (true/false) ";
    read grid;
    write "m = ";
    read m_hi;
    write "n = ";
    read n_hi;
    if grid then
        m <- 0;
        n <- 0;
    else
        m <- m_hi;
        n <- n_hi;
    fi
    while m <= m_hi do
        while n <= n_hi do
            call A(m, n, v);
            write "A(";
            write m;
            write ", ";
            write n;
            write ") = ";
            write v;
            write "; \t";
            n <- n + 1;
        od
        writeln "";
        m <- m + 1;
        n <- 0;
    od
}

procedure A (integer val m, integer val n, integer ret)
    integer tmp;
{
    if m = 0 then
        ret <- n + 1;
    else
        if n = 0 then
            call A(m - 1, 1, ret);
        else
            call A(m, n - 1, tmp);
            call A(m - 1, tmp, ret);
        fi
    fi
}
